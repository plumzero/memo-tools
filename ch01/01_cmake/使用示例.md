
## 基本用法
- 常用命令选项
    + -G "Unix Makefiles"
    + -DCMAKE_BUILD_TYPE=Release
    + -DPISTACHE_BUILD_EXAMPLES=true
    + -DPISTACHE_BUILD_TESTS=true
    + -DCMAKE_INSTALL_PREFIX=/usr/local/thislib
    + -DBUILD_SHARED_LIBS=on
- 版本要求
  ```cmake
    cmake_minimum_required(VERSION 2.6)
    cmake_minimum_required(VERSION 3.0 FATAL_ERROR)
  ```
- 支持 gdb
  ```cmake
       set(CMAKE_BUILD_TYPE "Debug")
       set(CMAKE_CXX_FLAGS_DEBUG "$ENV{CXXFLAGS} -O0 -Wall -g -ggdb")
       set(CMAKE_CXX_FLAGS_RELEASE "$ENV{CXXFLAGS} -O3 -Wall")
  ```
  之后可以直接对生成的程序使用 gdb 来调试。 
- 查看 GCC 版本
  ```cmake
    execute_process(COMMAND ${CMAKE_C_COMPILER} -dumpversion
                    OUTPUT_VARIABLE GCC_VERSION)

    message("CMAKE_C_COMPILER="${CMAKE_C_COMPILER})
    message("GCC_VERSION="${GCC_VERSION})
  ```
  输出如下:
  ```shell
    CMAKE_C_COMPILER=/usr/bin/cc
    GCC_VERSION=6.3.0
  ```
- 正则匹配及读取文件
  ```cmake
    file(READ ${CURL_SOURCE_DIR}/include/curl/curlver.h CURL_VERSION_H_CONTENTS)
    string(REGEX MATCH "#define LIBCURL_VERSION \"[^\"]*"
      CURL_VERSION ${CURL_VERSION_H_CONTENTS})
    string(REGEX REPLACE "[^\"]+\"" "" CURL_VERSION ${CURL_VERSION})
    string(REGEX MATCH "#define LIBCURL_VERSION_NUM 0x[0-9a-fA-F]+"
      CURL_VERSION_NUM ${CURL_VERSION_H_CONTENTS})
    string(REGEX REPLACE "[^0]+0x" "" CURL_VERSION_NUM ${CURL_VERSION_NUM})
  ```
- 信息打印
  ```cmake
    message(WARNING "the curl cmake build system is poorly maintained. Be aware")
    message(STATUS "curl version=[${CURL_VERSION}]")
    message(FATAL_ERROR
            "CA path only supported by OpenSSL, GnuTLS or mbed TLS. "
            "Set CURL_CA_PATH=none or enable one of those TLS backends.")
  ```
- 对多个字符串变量的连接使用
  ```cmake
    set(WARNING_BORDER "*******************************************************\n")
    set(NULL_ENTROPY_WARN_L1 "****  WARNING!  MBEDTLS_TEST_NULL_ENTROPY defined!\n")
    set(NULL_ENTROPY_WARN_L2 "****  THIS BUILD HAS NO DEFINED ENTROPY SOURCES\n")
    set(NULL_ENTROPY_WARN_L3 "****  AND IS *NOT* SUITABLE FOR PRODUCTION USE\n")
    set(NULL_ENTROPY_WARNING "${WARNING_BORDER}"
                           "${NULL_ENTROPY_WARN_L1}"
                           "${NULL_ENTROPY_WARN_L2}"
                           "${NULL_ENTROPY_WARN_L3}"
                           "${WARNING_BORDER}")
    if(${result} EQUAL 0)
        message(WARNING ${NULL_ENTROPY_WARNING})
    endif()
  ```
  
## 其他用法
- 定义一个函数，并在其中执行进程命令
  ```cmake
     function(link_to_source base_name)                                                   # 函数名称为 link_to_source, 传入参数为 base_name
         file(TO_NATIVE_PATH "${CMAKE_CURRENT_BINARY_DIR}/${base_name}" link)             # 将 cmake-style 路径转化为与当前平台相关路径，并存入 link
         file(TO_NATIVE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/${base_name}" target)

         if (NOT EXISTS ${link})
             if (CMAKE_HOST_UNIX)                                                         # 根据平台使用不同的命令
                 set(command ln -s ${target} ${link})                                     # command 是个 Normal 变量，将要执行的命令存储在其中
             else()
                 if (IS_DIRECTORY ${target})
                     set(command cmd.exe /c mklink /j ${link} ${target})
                 else()
                     set(command cmd.exe /c mklink /h ${link} ${target})
                 endif()
             endif()

             execute_process(COMMAND ${command}                                           # 执行命令，返回值 记录于 result，错误（如果有的话）打印到标准错误输出
                 RESULT_VARIABLE result
                 ERROR_VARIABLE output)

             if (NOT ${result} EQUAL 0)                                                   # 对返回值进行判断，以确定是否打印 output 的错误消息
                 message(FATAL_ERROR "Could not create symbolic link for: ${target} --> ${output}")
             endif()
         endif()
       endfunction(link_to_source)
  ```
- 对编译器版本的判断
  ```cmake
    string(REGEX MATCH "Clang" CMAKE_COMPILER_IS_CLANG "${CMAKE_C_COMPILER_ID}")
    string(REGEX MATCH "GNU" CMAKE_COMPILER_IS_GNU "${CMAKE_C_COMPILER_ID}")
    string(REGEX MATCH "IAR" CMAKE_COMPILER_IS_IAR "${CMAKE_C_COMPILER_ID}")
    string(REGEX MATCH "MSVC" CMAKE_COMPILER_IS_MSVC "${CMAKE_C_COMPILER_ID}")
    if(CMAKE_COMPILER_IS_GNU)
        execute_process(COMMAND ${CMAKE_C_COMPILER} -dumpversion
                        OUTPUT_VARIABLE GCC_VERSION)
        set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -Wextra -W -Wdeclaration-after-statement -Wwrite-strings")
        if (GCC_VERSION VERSION_GREATER 4.5 OR GCC_VERSION VERSION_EQUAL 4.5)
            set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wlogical-op")
        endif()
        if (GCC_VERSION VERSION_GREATER 4.8 OR GCC_VERSION VERSION_EQUAL 4.8)
            set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wshadow")
        endif()
      set(CMAKE_C_FLAGS_RELEASE     "-O2")
      set(CMAKE_C_FLAGS_DEBUG       "-O0 -g3")
      set(CMAKE_C_FLAGS_COVERAGE    "-O0 -g3 --coverage")
      set(CMAKE_C_FLAGS_ASAN        "-Werror -fsanitize=address -fno-common -O3")
      set(CMAKE_C_FLAGS_ASANDBG     "-Werror -fsanitize=address -fno-common -O1 -g3 -fno-omit-frame-pointer -fno-optimize-sibling-calls ")
      set(CMAKE_C_FLAGS_CHECK       "-Werror -Os")
      set(CMAKE_C_FLAGS_CHECKFULL   "${CMAKE_C_FLAGS_CHECK} -Wcast-qual")
    endif(CMAKE_COMPILER_IS_GNU)
  ```
- 开启 zlib 支持
  ```cmake
    option(ENABLE_ZLIB_SUPPORT "Build mbed TLS with zlib library." ON)     # 启用 zlib 支持
    if(ENABLE_ZLIB_SUPPORT)
       find_package(ZLIB)

       if(ZLIB_FOUND)
           include_directories(${ZLIB_INCLUDE_DIR})
       endif(ZLIB_FOUND)
    endif(ENABLE_ZLIB_SUPPORT)
  ```
- 静态库的编译和安装
  ```cmake
    option(USE_STATIC_MBEDTLS_LIBRARY "Build mbed TLS static library." ON)
    if(USE_STATIC_MBEDTLS_LIBRARY)
        add_library(${mbedcrypto_static_target} STATIC ${src_crypto})
        set_target_properties(${mbedcrypto_static_target} PROPERTIES OUTPUT_NAME mbedcrypto)
        target_link_libraries(${mbedcrypto_static_target} ${libs})

        add_library(${mbedx509_static_target} STATIC ${src_x509})
        set_target_properties(${mbedx509_static_target} PROPERTIES OUTPUT_NAME mbedx509)
        target_link_libraries(${mbedx509_static_target} ${libs} ${mbedcrypto_static_target})

        add_library(${mbedtls_static_target} STATIC ${src_tls})
        set_target_properties(${mbedtls_static_target} PROPERTIES OUTPUT_NAME mbedtls)
        target_link_libraries(${mbedtls_static_target} ${libs} ${mbedx509_static_target})

        install(TARGETS ${mbedtls_static_target} ${mbedx509_static_target} ${mbedcrypto_static_target}
               DESTINATION ${LIB_INSTALL_DIR}
               PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)
    endif(USE_STATIC_MBEDTLS_LIBRARY)
  ```
- 动态库的编译和安装
  ```cmake
    option(USE_SHARED_MBEDTLS_LIBRARY "Build mbed TLS shared library." OFF)
    if(USE_SHARED_MBEDTLS_LIBRARY)
        add_library(mbedcrypto SHARED ${src_crypto})
        set_target_properties(mbedcrypto PROPERTIES VERSION 2.12.0 SOVERSION 3)
        target_link_libraries(mbedcrypto ${libs})

        add_library(mbedx509 SHARED ${src_x509})
        set_target_properties(mbedx509 PROPERTIES VERSION 2.12.0 SOVERSION 0)
        target_link_libraries(mbedx509 ${libs} mbedcrypto)

        add_library(mbedtls SHARED ${src_tls})
        set_target_properties(mbedtls PROPERTIES VERSION 2.12.0 SOVERSION 11)
        target_link_libraries(mbedtls ${libs} mbedx509)

        install(TARGETS mbedtls mbedx509 mbedcrypto
              DESTINATION ${LIB_INSTALL_DIR}
              PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)
    endif(USE_SHARED_MBEDTLS_LIBRARY)
  ```
- 头文件安装
  ```cmake
    option(INSTALL_MBEDTLS_HEADERS "Install mbed TLS headers." ON)
    if(INSTALL_MBEDTLS_HEADERS)
        file(GLOB headers "mbedtls/*.h")            # 为 mbedtls 目录下的 .h 文件创建一个列表，存入 headers 变量中

        install(FILES ${headers}
            DESTINATION include/mbedtls
            PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ)
    endif(INSTALL_MBEDTLS_HEADERS)
  ```
